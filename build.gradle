// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.50'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        def nav_version = "2.1.0"

        classpath 'com.android.tools.build:gradle:3.5.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
    }

    ext {
        epoxy_version = '3.8.0'
        lifecycle_version = "2.2.0-rc02"
        room_version = "2.2.1"
        nav_version_ktx = "2.2.0-rc02"
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.25.0"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

ext {
    // query git for the commit count to automate versioning.
    gitCommitCount = 100 +
            Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'

    spotless {
        format 'misc', {
            target project.fileTree(project.rootDir) {
                include '**/*.gradle', '**/*.xml', '**/*.md', '**/*.gitignore'
                exclude '.idea/**/*.*'
            }

            trimTrailingWhitespace()
            endWithNewline()
        }
        kotlin {
            target "**/*.kt"
            ktlint()
            trimTrailingWhitespace()
            endWithNewline()
        }
    }
}
